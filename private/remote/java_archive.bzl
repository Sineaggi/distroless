BUILD_TMPL = """\
# GENERATED BY java_archive.bzl
load("@distroless//private/pkg:dpkg_status.bzl", "dpkg_status")
load("@distroless//private/pkg:debian_spdx.bzl", "debian_spdx")
load("@distroless//private/util:merge_providers.bzl", "merge_providers")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

pkg_tar(
    name = "data",
    srcs = glob(
        ["output/**/*"],
        exclude = [
            "output/bin/javac",
            "output/bin/javap",
            "output/bin/jdeprscan",
            "output/bin/jdeps",
            "output/bin/jfr",
            "output/bin/jimage",
            "output/bin/jlink",
            "output/bin/jmod",
            "output/bin/jpackage",
            "output/bin/jrunscript",
            "output/bin/jwebserver",
            "output/bin/rmiregistry",
            "output/bin/serialver",
        ],
    ),
    package_dir = "/java",
    strip_prefix = "external/{name}/output"
)

pkg_tar(
    name = "_control",
    srcs = ["control"]
)

dpkg_status(
    name = "dpkg",
    control = ":_control.tar",
    package_name = "{package_name}"
)

pkg_tar(
    name = "data_with_dpkg_status",
    deps = [
        ":data",
        ":dpkg"
    ]
)

debian_spdx(
    name = "spdx",
    control = ":_control.tar",
    data = ":data.tar",
    package_name = "{package_name}",
    spdx_id = "{spdx_id}",
    sha256 = "{sha256}",
    urls = [{urls}]
)

merge_providers(
    name = "{name}",
    srcs = [":data_with_dpkg_status", ":spdx"],
    visibility = ["//visibility:public"],
)
"""

def _impl(rctx):
    major_version = rctx.attr.version.split(".")[0]
    if (rctx.os.name == "mac os x" and rctx.os.arch == "x86_64"):
        source_jdk = Label("@corretto_jdk" + major_version + "_macos_amd64//:WORKSPACE")
    elif (rctx.os.name == "mac os x" and rctx.os.arch == "aarch64"):
        source_jdk = Label("@corretto_jdk" + major_version + "_macos_arm64//:WORKSPACE")
    elif (rctx.os.name == "linux" and rctx.os.arch == "x86_64"):
        source_jdk = Label("@corretto_jdk" + major_version + "_linux_amd64//:WORKSPACE")
    elif (rctx.os.name == "linux" and rctx.os.arch == "aarch64"):
        source_jdk = Label("@corretto_jdk" + major_version + "_linux_arm64//:WORKSPACE")
    else:
        fail("Unknown os/arch: " + rctx.os.name + "/" + rctx.os.arch)
    source_java_path = "/".join(str(rctx.path(source_jdk)).split("/")[:-1] + ["bin"])
    res = rctx.execute(
        [
            source_java_path + "/java",
            "--list-modules",
        ],
    )
    if res.return_code != 0:
        fail("Failed list java modules. stdout=\"" + res.stdout + "\", stderr=\"" + res.stderr + "\"")
    all_modules = [x.split("@")[0] for x in res.stdout.split("\n") if x]
    modules = [x for x in all_modules if x not in rctx.attr.excluded_modules]
    target_java_path = "/".join(str(rctx.path(rctx.attr.target_jdk)).split("/")[:-1])
    res = rctx.execute(
        [
            source_java_path + "/jlink",
            "--add-modules",
            ",".join(modules),
            "--module-path",
            rctx.path(target_java_path).get_child("jmods"),
            "--compress=" + rctx.attr.compress,
            "--no-header-files",
            "--no-man-pages",
            "--output",
            "output",
        ],
    )
    if res.return_code != 0:
        fail("Error: Failed to run jlink\n" + res.stdout)
    rctx.template(
        "control",
        rctx.attr.control,
        substitutions = {
            "{{VERSION}}": rctx.attr.version,
            "{{ARCHITECTURE}}": rctx.attr.architecture,
        },
    )
    rctx.file(
        "BUILD.bazel",
        content = BUILD_TMPL.format(
            name = rctx.attr.name,
            package_name = rctx.attr.package_name,
            spdx_id = rctx.attr.name,
            urls = ",".join(['"%s"' % url for url in rctx.attr.urls]),
            sha256 = rctx.attr.sha256,
        ),
    )

EXCLUDED_MODULES = [
    "jdk.attach",
    "jdk.compiler",
    "jdk.editpad",
    "jdk.hotspot.agent",
    "jdk.internal.jvmstat",
    "jdk.internal.opt",
    "jdk.jartool",  # jar tool
    "jdk.javadoc",  # javadoc tool
    "jdk.jcmd",
    "jdk.jconsole",
    "jdk.jdeps",  # jdeps tool, scans modules in dependencies
    "jdk.jdi",
    "jdk.jlink",  # doesn't work
    "jdk.jshell",  # java shell
    "jdk.jstatd",
    "jdk.pack",  # deprecated (and removed in java 14) tool for packaging jars in pack2000 format
    "jdk.rmic",
    "jdk.unsupported.desktop",
]

java_archive = repository_rule(
    implementation = _impl,
    attrs = {
        "urls": attr.string_list(mandatory = True),
        "sha256": attr.string(mandatory = True),
        "type": attr.string(default = ".tar.gz"),
        "package_name": attr.string(default = "java"),
        "version": attr.string(mandatory = True),
        "architecture": attr.string(mandatory = True),
        "control": attr.label(),
        "target_jdk": attr.label(mandatory = True),
        "compress": attr.string(default = "zip-6"),
        "excluded_modules": attr.string_list(default = EXCLUDED_MODULES),
    },
)
